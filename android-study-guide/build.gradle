buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.google.gms:google-services:4.3.13'
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
  }
}

plugins {
  id 'com.google.dagger.hilt.android' version '2.44' apply false
  id 'com.android.application' version '7.3.1' apply false
  id 'com.android.library' version '7.3.1' apply false
  id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
  id 'com.google.devtools.ksp' version '1.7.10-1.0.6' apply false
}

subprojects {
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      if (project.findProperty("dev.goobar.enableComposeCompilerReports") == "true") {
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" + project.buildDir.absolutePath + "/compose_metrics"
        ]
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" + project.buildDir.absolutePath + "/compose_metrics"
        ]
      }
    }
  }
}